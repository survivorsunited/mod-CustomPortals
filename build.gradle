plugins {
	id 'fabric-loom' version "$loom_version"
	id 'maven-publish'
}

def mcVersion = project.minecraft_version
def useLevelLoadingScreen = mcVersion.startsWith("1.21.") && Integer.parseInt(mcVersion.substring("1.21.".length())) >= 9

if (useLevelLoadingScreen) {
	sourceSets.main.java.srcDirs += "src/main/java-1.21.9"
	sourceSets.main.java.exclude "**/DownloadingTerrainScreenMixin.java"
	sourceSets.main.java.exclude "**/ServerPlayerEntityMixin.java"
	sourceSets.main.java.exclude "**/CPParticles.java"
	sourceSets.main.java.exclude "**/PortalBlock5Arg.java"
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven {
        name = 'Ladysnake Mods'
        url = 'https://maven.ladysnake.org/releases'
    }
	maven {
        url 'https://jitpack.io'
    }
    maven {
        url "https://maven.shedaniel.me/"
    }
    maven {
        url "https://maven.terraformersmc.com/"
    }
	maven {
		url "https://maven.siphalor.de/"
	}
	maven {
		name 'Xander Maven'
		url 'https://maven.isxander.dev/releases'
	}

}

dependencies {
	minecraft "com.mojang:minecraft:$minecraft_version"
	mappings "net.fabricmc:yarn:$yarn_mappings"
	modImplementation "net.fabricmc:fabric-loader:$loader_version"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_version"

	// Adds an API dependency on the base cardinal components module (required by every other module)
	modApi "org.ladysnake.cardinal-components-api:cardinal-components-base:$cardinalcomponents_version"
	include "org.ladysnake.cardinal-components-api:cardinal-components-base:$cardinalcomponents_version"
	// Replace modImplementation with modApi if you expose components in your own API
	modImplementation "org.ladysnake.cardinal-components-api:cardinal-components-world:$cardinalcomponents_version"
	include "org.ladysnake.cardinal-components-api:cardinal-components-world:$cardinalcomponents_version"

	modImplementation "dev.isxander:yet-another-config-lib:$yacl_version-fabric"

	modImplementation("com.terraformersmc:modmenu:$modmenu_version")

	// PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
	// You may need to force-disable transitiveness on them.
}

processResources {
	inputs.property "version", project.version
	inputs.property "minecraft_version", project.minecraft_version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}

	doLast {
		def mixins = useLevelLoadingScreen
			? ["EntityMixin", "ServerPlayerEntityMixin21_9", "TeleportTargetAccessor"]
			: ["EntityMixin", "ServerPlayerEntityMixin", "TeleportTargetAccessor"]
		def client = useLevelLoadingScreen
			? ["ClientPlayerEntityMixin", "InGameHudMixin", "LevelLoadingScreenMixin"]
			: ["ClientPlayerEntityMixin", "InGameHudMixin", "DownloadingTerrainScreenMixin"]
		def config = [
			required: true,
			minVersion: "0.8",
			package: "dev.custom.portals.mixin",
			compatibilityLevel: "JAVA_21",
			mixins: mixins,
			client: client,
			server: [],
			injectors: [defaultRequire: 1]
		]
		file("${destinationDir}/customportals.mixins.json").text = groovy.json.JsonOutput.toJson(config)
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"

	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

loom {
	runs {
		testClient1 {
			inherit client
			runDir = "run1"
			configName = "Test Minecraft Client 1"
		}
		testClient2 {
			inherit client
			runDir = "run2"
			configName = "Test Minecraft Client 2"
		}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}
