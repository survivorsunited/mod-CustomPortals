plugins {
	id 'fabric-loom' version "$loom_version"
	id 'maven-publish'
}

// Support both jar_name (for pipeline) and archives_base_name
def jarName = project.hasProperty("jar_name") ? project.jar_name : project.archives_base_name
archivesBaseName = jarName
version = project.mod_version
group = project.maven_group

repositories {
    maven {
        name = 'Ladysnake Mods'
        url = 'https://maven.ladysnake.org/releases'
    }
	maven {
        url 'https://jitpack.io'
    }
    maven {
        url "https://maven.shedaniel.me/"
    }
    maven {
        url "https://maven.terraformersmc.com/"
    }
	maven {
		url "https://maven.siphalor.de/"
	}
	maven {
		name 'Xander Maven'
		url 'https://maven.isxander.dev/releases'
	}

}

dependencies {
	minecraft "com.mojang:minecraft:$minecraft_version"
	mappings "net.fabricmc:yarn:$yarn_mappings"
	modImplementation "net.fabricmc:fabric-loader:$loader_version"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_version"

	modImplementation "dev.isxander:yet-another-config-lib:$yacl_version-fabric"

	modImplementation("com.terraformersmc:modmenu:$modmenu_version")

	// PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
	// You may need to force-disable transitiveness on them.
}

processResources {
	inputs.property "mod_version", project.mod_version
	inputs.property "mod_id", project.mod_id
	inputs.property "mod_name", project.mod_name
	inputs.property "mod_description", project.mod_description
	inputs.property "mod_author", project.mod_author
	inputs.property "mod_license", project.mod_license
	inputs.property "mod_homepage", project.mod_homepage
	inputs.property "mod_sources", project.mod_sources
	inputs.property "mod_issues", project.mod_issues
	inputs.property "mod_icon", project.mod_icon
	inputs.property "main_class", project.main_class
	inputs.property "client_class", project.client_class
	inputs.property "modmenu_class", project.modmenu_class
	inputs.property "minecraft_version", project.minecraft_version
	inputs.property "fabric_loader_version", project.fabric_loader_version
	inputs.property "java_version", project.java_version
	inputs.property "fabric_api_version", project.fabric_api_version
	inputs.property "modmenu_version", project.modmenu_version

	filteringCharset "UTF-8"

	filesMatching("fabric.mod.json") {
		expand "mod_version": project.mod_version,
		       "mod_id": project.mod_id,
		       "mod_name": project.mod_name,
		       "mod_description": project.mod_description,
		       "mod_author": project.mod_author,
		       "mod_license": project.mod_license,
		       "mod_homepage": project.mod_homepage,
		       "mod_sources": project.mod_sources,
		       "mod_issues": project.mod_issues,
		       "mod_icon": project.mod_icon,
		       "main_class": project.main_class,
		       "client_class": project.client_class,
		       "modmenu_class": project.modmenu_class,
		       "minecraft_version": project.minecraft_version,
		       "fabric_loader_version": project.fabric_loader_version,
		       "java_version": project.java_version,
		       "fabric_api_version": project.fabric_api_version,
		       "modmenu_version": project.modmenu_version
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"

	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

loom {
	runs {
		testClient1 {
			inherit client
			runDir = "run1"
			configName = "Test Minecraft Client 1"
		}
		testClient2 {
			inherit client
			runDir = "run2"
			configName = "Test Minecraft Client 2"
		}
	}
}

// Task to print property values for CI/CD
task printProperty {
	doLast {
		if (project.hasProperty("property")) {
			def prop = project.property("property")
			if (project.hasProperty(prop)) {
				println project.property(prop)
			} else {
				throw new GradleException("Property '${prop}' not found")
			}
		} else {
			throw new GradleException("Usage: gradle printProperty -Pproperty=<property_name>")
		}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}
